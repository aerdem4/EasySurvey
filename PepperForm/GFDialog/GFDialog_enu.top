topic: ~GFDialog()
language: enu

dynamic:question
dynamic:choices

concept:(nextQuestion) ["And the question is!" "What's next?" "Let's see." "Let's go on."]
concept:(checked) ["You can select one more or say next to go to the next question." "Say next or give another choice." "Say next or give another choice" "Say next or give another choice"]

u:(in:onActivation) Welcome to Pepper Form. Let's start! ^gotoReactivate(nextQuestion)

proposal: %nextQuestion ~nextQuestion ^call(GoogleForm.nextQuestion())
    c1:(ss-checkbox) ^gotoReactivate(ss-checkbox)
    c1:(["ss-text" "ss-paragraph-text"]) ^gotoReactivate(ss-text)
    c1:(["ss-select" "ss-radio"]) ^gotoReactivate(ss-radio)
    c1:(finished) ^gotoReactivate(end)

proposal: %ss-text $GoogleForm/Question
    u1:(_*) I see. Very interesting! ^call(GoogleForm.answer( $1 )) ^gotoReactivate(nextQuestion)

proposal: %ss-radio $GoogleForm/Question
    u1:(_~choices) Good choice! \pau=500\ I think. ^call(GoogleForm.answer( $1 )) ^gotoReactivate(nextQuestion)

proposal: %ss-checkbox $GoogleForm/Question
    u1:(_~choices) Ok. ^call(GoogleForm.addCheckbox( $1 )) ~checked ^stayInScope
    u1:(next) All right. ^call(GoogleForm.answerCheckbox()) ^gotoReactivate(nextQuestion)

proposal: %end Oh! It's finished! Thank you for your time! $onStopped=1

